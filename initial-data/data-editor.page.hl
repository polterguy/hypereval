/*
 * Creates a form that allows the user to edit contents in p5.data database.
 *
 * Requires you to be logged in as "root", but will ask the user to login,
 * if he is not already logged in.
 */


/*
 * Verifying user is logged in as root, and if not, forcing the user
 * to login before we proceed.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in as root.
   */
  p5.core.login
    message:You'll need to login with a root account to edit your p5.data database.

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return


/*
 * Creating main wire frame.
 */
create-widget
  class:container
  oninit

    /*
     * Including Micro CSS.
     */
    micro.css.include

    /*
     * Setting initial [offset] and [filter] for grid.
     */
    p5.web.viewstate.set:hypereval.data-editor.offset
      src:int:0
    p5.web.viewstate.set:hypereval.data-editor.filter
      src:

  widgets
    div
      class:row
      widgets
        div
          class:col
          widgets

            /*
             * Descriptive header.
             */
            h1
              class:center air-top
              innerValue:p5.data records

            /*
             * Main content wrapper, creates some chrome wrapping entire app.
             */
            div:hypereval-data-editor-main-wrapper
              class:bg air-inner shaded rounded
              style:"overflow:auto;"
              widgets

                /*
                 * Datagrid containing our p5.data records.
                 */
                micro.widgets.grid
                  class:hover striped
                  columns
                    Name
                      style:"width:50%;"
                    ID
                      style:"width:50%;"
                  oninit

                    /*
                     * Initial databinding of grid.
                     */
                    hypereval._database-editor.databind

                  events


                    /*
                     * Databinds grid of items from p5.data.
                     *
                     * Optionally pass in [offset] and [filter].
                     */
                    hypereval._database-editor.databind

                      /*
                       * Defaults, if no arguments are given.
                       */
                      .defaults
                        offset:int:0

                      /*
                       * Sanity checking arguments.
                       */
                      micro.lambda.contract.optional:x:/..
                        filter:string
                        offset:int

                      /*
                       * Calculating slice to select.
                       */
                      +:x:(/../*/offset|/@.defaults/*/offset)/$?value.int
                        _:int:10

                      /*
                       * Creating our (optional) filter condition.
                       */
                      _filter
                      if:x:/../*/filter?value
                        and:x:/@../*/filter?value
                          !=:
                        set:x:/@_filter?value
                          src:@"(/""~{0}""|/""=~{0}""/*/""~{0}""/.|/*/""=~{0}""/.)"
                            :x:/../*/filter?value

                      /*
                       * Querying database.
                       */
                      select-data:x:/*/*{0}/[{1},{2}]
                        :x:/@_filter?value
                        :x:(/../*/offset|/@.defaults/*/offset)/$?value.int
                        :x:/@+?value

                      /*
                       * Looping through each result set from above, creating one item for our databind operation.
                       */
                      for-each:x:/@select-data/*
                        eval-x:x:/+/*/*/*|/+/**/.id
                        add:x:/../*/micro.widgets.grid.databind
                          src
                            item
                              Name:x:/@_dp/#?name
                              ID:x:/@_dp/#?value
                              .row
                                onclick

                                  /*
                                   * ID for item, forward evaluated above.
                                   */
                                  .id:x:/@_dp/#?value

                                  /*
                                   * Making sure we pass in correct type into [select-data].
                                   */
                                  p5.types.get-typename:x:/@.id?value
                                  select-data:x:@"/*/*/""=:{0}:{1}"""
                                    :x:/@p5.types.get-typename?value
                                    :x:/@.id?value

                                  /*
                                   * Creating our editor widget.
                                   */
                                  lambda2hyper:x:/@select-data/*
                                  p5.types.get-typename:x:/@select-data/*?value
                                  create-widget
                                    parent:hypereval-data-editor-main-wrapper
                                    class:air-top
                                    .editor:x:/@.id?value
                                    widgets
                                      hr
                                      input
                                        type:hidden
                                        .type
                                        value:"{0}:{1}:{2}"
                                          :x:/@select-data/*?name
                                          :x:/@p5.types.get-typename?value
                                          :x:/@select-data/*?value
                                      micro.widgets.codemirror
                                        value:x:/@lambda2hyper?value
                                      div
                                        class:right air-top
                                        widgets
                                          div
                                            class:strip
                                            widgets
                                              button
                                                innerValue:@"<span class=""icon-floppy-disk""></span>"
                                                class:larger
                                                title:Saves your (updated) record
                                                onclick

                                                  /*
                                                   * Making sure we're able to trap errors.
                                                   */
                                                  try

                                                    /*
                                                     * Finds our textarea widget, transforming its content to Hyperlambda,
                                                     * and saves our record.
                                                     *
                                                     * First getting new lambda content for item.
                                                     */
                                                    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                                                      .editor
                                                    p5.web.widgets.find:x:/-/*/*?value
                                                      element:textarea
                                                    get-widget-property:x:/-/*/*?value
                                                      value
                                                    hyper2lambda:x:/-/*/*?value

                                                    /*
                                                     * Then getting our type and the ID of our record.
                                                     */
                                                    p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                                                      .type
                                                    get-widget-property:x:/-/*/*?value
                                                      value
                                                    split:x:/-/*/*?value
                                                      =:":"

                                                    /*
                                                     * Updating item, making sure we parametrise our [update-data] invocation.
                                                     */
                                                    add:x:/..try/*/update-data/*/src
                                                      src:x:/@hyper2lambda/*
                                                    update-data:x:@"/*/*/""=:{0}:{1}"""
                                                      :x:/@split/1?name
                                                      :x:/@split/2?name
                                                      src

                                                    /*
                                                     * Giving user feedback about.
                                                     */
                                                    micro.windows.info:Record was successfully updated
                                                      class:micro-windows-info success

                                                  catch

                                                    /*
                                                     * Giving user feedback about error.
                                                     */
                                                    micro.windows.info:x:/@message?value
                                                      class:micro-windows-info warning

                                              button
                                                innerValue:@"<span class=""icon-bin""></span>"
                                                title:Delete this record
                                                onclick

                                                  /*
                                                   * Asking user to confirm action.
                                                   */
                                                  create-widgets
                                                    micro.widgets.modal:hypereval-data-editor-confirm-deletion
                                                      widgets
                                                        h3
                                                          innerValue:Please confirm action
                                                        p
                                                          innerValue:Are you sure you want to delete this record? This action is permanent.
                                                        div
                                                          class:right
                                                          widgets
                                                            div
                                                              class:strip
                                                              widgets
                                                                button
                                                                  innerValue:Yes
                                                                  oninit

                                                                    /*
                                                                     * Setting initial focus to "Yes" button.
                                                                     */
                                                                    micro.page.set-focus:x:/../*/_event?value

                                                                  onclick

                                                                    /*
                                                                     * Forward evaluated above.
                                                                     */
                                                                    .id:x:/@_dp/#?value

                                                                    /*
                                                                     * Deleting data record.
                                                                     */
                                                                    p5.types.get-typename:x:/@.id?value
                                                                    delete-data:x:@"/*/*/""=:{0}:{1}"""
                                                                      :x:/@p5.types.get-typename?value
                                                                      :x:/@.id?value

                                                                    /*
                                                                     * Re-databinding grid.
                                                                     */
                                                                    p5.web.viewstate.get:hypereval.data-editor.filter
                                                                    p5.web.viewstate.get:hypereval.data-editor.offset
                                                                    eval-x:x:/+/*
                                                                    hypereval._database-editor.databind
                                                                      filter:x:/../*/p5.web.viewstate.get/[0,1]/*?value
                                                                      offset:x:/../*/p5.web.viewstate.get/[1,2]/*?value

                                                                    /*
                                                                     * Finding our root editor widget, and simply deleting it, in addition
                                                                     * to deleting our modal window.
                                                                     */
                                                                    p5.web.widgets.find
                                                                      .editor:x:/@.id?value
                                                                    delete-widget:x:/-/*/*?value
                                                                    delete-widget:hypereval-data-editor-confirm-deletion

                                                                button
                                                                  innerValue:No
                                                                  onclick

                                                                    /*
                                                                     * Deleting modal confirmation window.
                                                                     */
                                                                    delete-widget:hypereval-data-editor-confirm-deletion

                                              button
                                                innerValue:@"<span class=""icon-cross""></span>"
                                                title:Close the editor for this record
                                                onclick

                                                  /*
                                                   * Finding our root editor widget, and simply deleting it.
                                                   */
                                                  p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                                                    .editor
                                                  delete-widget:x:/-/*/*?value


                      /*
                       * Actual databind operation.
                       */
                      micro.widgets.grid.databind:x:/../*/_event?value

                      /*
                       * Checking if there are (possibly) more records.
                       */
                      if:x:/@select-data/*?count
                        =:int:10
                        return:bool:true
                      return:bool:false


                div
                  class:strip right
                  widgets
                    input:hypereval-data-editor-search-filter
                      type:text
                      title:Filter, keyboard shortcut F
                      accesskey:F
                      class:smaller
                      onkeydown:@"if (event.keyCode == 13) {p5.$('hypereval-data-editor-search-btn').raise('onclick');return false;}"
                      oninit

                        /*
                         * Setting initial focus to filter textbox.
                         */
                        micro.page.set-focus:x:/../*/_event?value
                        select-data:x:/*/*?count
                        set-widget-property:x:/../*/_event?value
                          placeholder:{0} items ...
                            :x:/@select-data?value

                    button:hypereval-data-editor-search-btn
                      title:Search, keyboard shortcut S
                      innerValue:@"<span class=""icon-search""></span>"
                      accesskey:S
                      onclick

                        /*
                         * Retrieving filter, storing it in ViewState, and re-databinding datagrid.
                         */
                        get-widget-property:hypereval-data-editor-search-filter
                          value

                        /*
                         * Clearing our [offset] value.
                         */
                        p5.web.viewstate.set:hypereval.data-editor.offset
                          src:int:0

                        /*
                         * Making sure we disable our "previous button".
                         */
                        set-widget-property:hypereval-data-editor-previous-btn
                          disabled

                        /*
                         * Checking if filter is empty, and if so, making sure we remove the [filter]Â argument
                         * to our databind invocation, disabling our "clear filter" button, and removing
                         * our viewstate filter value.
                         */
                        if:x:/@get-widget-property/*/*?value
                          =:

                          /*
                           * Filter is empty.
                           */
                          set-widget-property:hypereval-data-editor-clear-btn
                            disabled
                          p5.web.viewstate.set:hypereval.data-editor.filter
                            src:

                        else

                          /*
                           * Filter was not empty.
                           */
                          p5.web.viewstate.set:hypereval.data-editor.filter
                            src:x:/@get-widget-property/*/*?value
                          delete-widget-property:hypereval-data-editor-clear-btn
                            disabled

                        /*
                         * Invoking our databind event.
                         */
                        eval-x:x:/+/*
                        hypereval._database-editor.databind
                          filter:x:/@get-widget-property/*/*?value
                        if:x:/-?value
                          =:bool:true
                          delete-widget-property:hypereval-data-editor-next-btn
                            disabled
                        else
                          set-widget-property:hypereval-data-editor-next-btn
                            disabled
                        micro.page.set-focus:hypereval-data-editor-search-filter

                    button:hypereval-data-editor-clear-btn
                      innerValue:@"<span class=""icon-cross""></span>"
                      accesskey:X
                      title:Clear filter, keyboard shortcut X
                      disabled
                      onclick

                        /*
                         * Clearing our viewstate offset value and filter condition, removing value
                         * from filter textbox, and re-databinding grid.
                         */
                        p5.web.viewstate.set:hypereval.data-editor.offset
                          src:int:0
                        p5.web.viewstate.set:hypereval.data-editor.filter
                          src:
                        set-widget-property:hypereval-data-editor-search-filter
                          value:
                        hypereval._database-editor.databind
                        if:x:/-?value
                          =:bool:true
                          delete-widget-property:hypereval-data-editor-next-btn
                            disabled

                        /*
                         * Disabling clear filter button, and previus button, and setting
                         * focus to filter textbox.
                         */
                        set-widget-property:hypereval-data-editor-clear-btn
                          disabled
                        set-widget-property:hypereval-data-editor-previous-btn
                          disabled
                        micro.page.set-focus:hypereval-data-editor-search-filter

                    button:hypereval-data-editor-previous-btn
                      innerValue:@"<span class=""icon-arrow-left""></span>"
                      accesskey:P
                      title:Go to previous page, keyboard shortcut P
                      disabled
                      onclick

                        /*
                         * Retrieving current offset, decrementing it by 10, and re-databinding grid.
                         */
                        p5.web.viewstate.get:hypereval.data-editor.offset
                        -:x:/@p5.web.viewstate.get/*?value.int
                          _:10
                        p5.web.viewstate.set:hypereval.data-editor.offset
                          src:x:/@-?value
                        p5.web.viewstate.get:hypereval.data-editor.filter
                        eval-x:x:/+/*
                        hypereval._database-editor.databind
                          filter:x:/@p5.web.viewstate.get/*?value
                          offset:x:/@-?value

                        /*
                         * Checking if we should disable previous button.
                         */
                        if:x:/@-?value.int
                          =:int:0
                          set-widget-property:hypereval-data-editor-previous-btn
                            disabled
                        delete-widget-property:hypereval-data-editor-next-btn
                          disabled

                    button:hypereval-data-editor-next-btn
                      innerValue:@"<span class=""icon-arrow-right""></span>"
                      accesskey:N
                      title:Go to next page, keyboard shortcut N
                      onclick

                        /*
                         * Retrieving current offset, incrementing it by 10, and re-databinding grid.
                         */
                        p5.web.viewstate.get:hypereval.data-editor.offset
                        +:x:/@p5.web.viewstate.get/*?value.int
                          _:10
                        p5.web.viewstate.set:hypereval.data-editor.offset
                          src:x:/@+?value
                        p5.web.viewstate.get:hypereval.data-editor.filter
                        eval-x:x:/+/*
                        hypereval._database-editor.databind
                          filter:x:/@p5.web.viewstate.get/*?value
                          offset:x:/@+?value
                        delete-widget-property:hypereval-data-editor-previous-btn
                          disabled

                        /*
                         * Checking if we are at end of dataset.
                         */
                        if:x:/@hypereval._database-editor.databind?value
                          not
                          set-widget-property:hypereval-data-editor-next-btn
                            disabled

                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      title:Create new record(s), keyboard shortcut C
                      accesskey:C
                      onclick

                        /*
                         * Showing a modal window, with a Hyperlambda editor, allowing
                         * user to create a new item.
                         */
                        create-widgets
                          micro.widgets.modal:hypereval-data-editor-new-record-modal
                            widgets
                              h3
                                innerValue:New records(s)
                              p
                                innerValue:Supply me with data for your record(s).
                              micro.widgets.codemirror
                                .data-field:content
                              div
                                class:right
                                widgets
                                  div
                                    class:strip
                                    widgets
                                      button
                                        innerValue:@"<span class=""icon-floppy-disk""></span>"
                                        class:larger
                                        title:Creates your new record(s)
                                        onclick

                                          /*
                                           * Making sure we wrap out insert into a try/catch block, in case of errors.
                                           */
                                          try

                                            /*
                                             * Retrieving Hyperlambda from Code-Mirror editor, and inserting
                                             * it into p5.data database.
                                             */
                                            micro.form.serialize:hypereval-data-editor-new-record-modal
                                            hyper2lambda:x:/@micro.form.serialize/*/content?value
                                            add:x:/..try/*/insert-data
                                              src:x:/@hyper2lambda/*
                                            insert-data

                                            /*
                                             * Deleting modal window.
                                             */
                                            delete-widget:hypereval-data-editor-new-record-modal

                                            /*
                                             * Re-databinding grid.
                                             */
                                            p5.web.viewstate.get:hypereval.data-editor.filter
                                            p5.web.viewstate.get:hypereval.data-editor.offset
                                            if:x:/../*/p5.web.viewstate.get/[0,1]/*?value
                                              not
                                              or:x:/../*/p5.web.viewstate.get/[0,1]/*?value
                                                =:
                                              set:x:/..try/*/hypereval._database-editor.databind/*/filter
                                            if:x:/../*/p5.web.viewstate.get/[1,2]/*?value
                                              not
                                              or:x:/../*/p5.web.viewstate.get/[1,2]/*?value
                                                =:
                                              set:x:/..try/*/hypereval._database-editor.databind/*/offset
                                            eval-x:x:/+/*
                                            hypereval._database-editor.databind
                                              filter:x:/../*/p5.web.viewstate.get/[0,1]/*?value
                                              offset:x:/../*/p5.web.viewstate.get/[1,2]/*?value

                                          catch

                                            /*
                                             * Displaying error to user.
                                             */
                                            micro.windows.info:x:/@message?value
                                              class:micro-windows-info warning

                                      button
                                        innerValue:@"<span class=""icon-cross""></span>"
                                        title:Close window, without creating new record(s)
                                        onclick

                                          /*
                                           * Deleting modal window.
                                           */
                                          delete-widget:hypereval-data-editor-new-record-modal

                    button
                      innerValue:@"<span class=""icon-eye""></span>"
                      accesskey:A
                      title:Advanced, keyboard shortcut A
                      onclick

                        /*
                         * Toggling advanced editor parts.
                         */
                        get-widget-property:hypereval-data-editor-advanced
                          visible
                        if:x:/-/*/*?value
                          =:bool:true

                          /*
                           * Hiding advanced widget.
                           */
                          set-widget-property:hypereval-data-editor-advanced
                            visible:false

                        else

                          /*
                           * Showing advanced widget.
                           */
                          set-widget-property:hypereval-data-editor-advanced
                            visible:true

                        /*
                         * Toggling "toggled" CSS class for button.
                         */
                        micro.css.toggle:x:/../*/_event?value
                          class:toggled

                    button
                      innerValue:@"<span class=""icon-download2""></span>"
                      title:Download a backup of your database
                      onclick

                        /*
                         * Loops through all database records, creating a backup zip file,
                         * and downloading it to client.
                         *
                         * First making sure our temporary folder exists, and that it is empty.
                         */
                        if
                          fetch:x:/0/0?value
                            folder-exists:~/temp/p5-data-backup/
                          not

                          /*
                           * Creating temp folder to hold zip file, and backup files.
                           */
                          create-folder:~/temp/p5-data-backup/

                        else

                          /*
                           * Deleting temp folder for any existing files and folders.
                           */
                          list-files:~/temp/p5-data-backup/
                          delete-file:x:/-/*?name
                          list-folders:~/temp/p5-data-backup/
                          delete-folder:x:/-/*?name

                        /*
                         * Looping through each database record, creating one Hyperlambda file, 
                         * for every 50th record.
                         */
                        .offset:int:0
                        .no:int:1
                        while:bool:true
                          +:x:/@.offset?value.int
                            _:50
                          select-data:x:/*/*/[{0},{1}]
                            :x:/@.offset?value
                            :x:/@+?value
                          if:x:/@select-data/*
                            not

                            /*
                             * No more records.
                             */
                            break

                          /*
                           * Transforming current result set to Hyperlambda, and stuffing it into a file,
                           * in our temporary folder.
                           */
                          lambda2hyper:x:/@select-data/*
                          save-file:"~/temp/p5-data-backup/db-{0:0000}.hl"
                            :x:/@.no?value
                            src:x:/@lambda2hyper?value
                          set:x:/@.no?value
                            +:x:/@.no?value
                              _:1
                          set:x:/@.offset?value
                            +:x:/@.offset?value
                              _:50

                        /*
                         * Zipping entire backup folder, and downloading to client.
                         *
                         * Making sure we delete any previously created backup zip files first.
                         */
                        if
                          fetch:x:/0/0?value
                            file-exists:~/temp/database-backup.zip
                          delete-file:~/temp/database-backup.zip

                        /*
                         * Creating zip file containing all p5.data records, 
                         * and downloading it to the client.
                         */
                        zip:~/temp/database-backup.zip
                          src:~/temp/p5-data-backup/
                        micro.download.file:~/temp/database-backup.zip

                div:hypereval-data-editor-advanced
                  visible:false
                  widgets
                    label
                      innerValue:Immediate mode
                    micro.widgets.codemirror:hypereval-data-editor-advanced-code
                    div
                      class:right air-top
                      widgets
                        button
                          class:larger
                          innerValue:@"<span class=""icon-flash""></span>"
                          title:Evaluate your code
                          onclick

                            /*
                             * Retrievines the code from our Hyperlambda editor, and evaluates it,
                             * before re-databinding our grid (since it was probably insert, delete,
                             * or something, affecting our current result set).
                             */
                            get-widget-property:hypereval-data-editor-advanced-code
                              innerValue
                            eval:x:/-/*/*?value
                            p5.web.viewstate.get:hypereval.data-editor.filter
                            if:x:/@p5.web.viewstate.get/*?value
                              not
                              or:x:/@p5.web.viewstate.get/*?value
                                =:
                              set:x:/../*/hypereval._database-editor.databind/*/filter
                            eval-x:x:/+/*
                            hypereval._database-editor.databind
                              filter:x:/../*/p5.web.viewstate.get/[0,1]/*?value

